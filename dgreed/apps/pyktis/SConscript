Import('env')

NAME='pyktis'
ASSETS_DIR = NAME + '_assets'
SCRIPTS_DIR = NAME + '_scripts'

sources = Glob('*.c', strings=True)

if str(Platform()) == 'win32':
	sources += [env.RES('resource.rc')]

app = env.Program(NAME + env['DGREED_POSTFIX'], sources, LIBS=env['MALKA_LIBS']\
	+env['DGREED_LIBS'])
env.Install('#'+env['DGREED_BIN_DIR'], app)

# Copy assets and scripts only in debug build
if env['DGREED_POSTFIX'] == 'd':
	dest_dir = '#' + env['DGREED_BIN_DIR'] + '/' + ASSETS_DIR
	for asset in Glob(ASSETS_DIR + '/*', strings=True):
		if asset.endswith('.fnt'):
			env.Command('#' + env['DGREED_BIN_DIR']+'/'+ asset.replace('.fnt', '.bft'), 
				asset, env['DGREED_FONT_TOOL'])
		elif asset.endswith('.png') or asset.endswith('.wav') or \
			asset.endswith('.ogg') or asset.endswith('.mml') or \
			asset.endswith('.bft') or asset.endswith('.ttf'):
			env.Install(dest_dir, asset)
	
	dest_dir = '#' + env['DGREED_BIN_DIR'] + '/' + SCRIPTS_DIR
	for script in Glob(SCRIPTS_DIR + '/*.lua', strings=True):
		env.Install(dest_dir, script)

