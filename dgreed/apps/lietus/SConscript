Import('env')

NAME='lietus'
ASSETS_DIR = NAME + '_assets'

sources = Glob('*.c', strings=True)
app = env.Program(NAME + env['DGREED_POSTFIX'], sources, LIBS=env['DGREED_LIBS'])
env.Install('#'+env['DGREED_BIN_DIR'], app)

# Copy assets only in debug build
if env['DGREED_POSTFIX'] == 'd':
	dest_dir = '#' + env['DGREED_BIN_DIR'] + '/' + ASSETS_DIR
	for asset in Glob(ASSETS_DIR + '/*', strings=True):
		if asset.endswith('.fnt'):
			env.Command('#' + env['DGREED_BIN_DIR']+'/'+ asset.replace('.fnt', '.bft'), 
				asset, env['DGREED_FONT_TOOL'])
		elif asset.endswith('.oel'):
			env.Command('#' + env['DGREED_BIN_DIR']+'/'+ asset.replace('.oel', '.btm'),
				[asset, ASSETS_DIR + '/lietus.oep'], 
				env['DGREED_OGMO_TOOL'] + ' -p lietus_assets/')
		elif asset.endswith('.png') or asset.endswith('.wav') or \
			asset.endswith('.ogg'):		
			env.Install(dest_dir, asset)

