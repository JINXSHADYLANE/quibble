import os
import tools.mml

BIN_DIR = 'bin'
BUILD_DIR = 'build_'
TOOL_DIR = 'tools'
SUBS = ['src', 'apps']

env = Environment(CCFLAGS='-DTRACK_MEMORY -Wall -std=c99', CPPPATH='#/src',
	LIBPATH='#/bin', DGREED_BIN_DIR=BIN_DIR)

default_font_tool = TOOL_DIR + '/' + 'fnt2bft.py $SOURCE $TARGET' 	
default_test_tool = TOOL_DIR + '/' + 'gen_tests.py -o $TARGET $SOURCES'
default_arenacompr_tool = TOOL_DIR + '/' + 'greed_arena_compress.py $SOURCE $TARGET'
if str(Platform()) == 'win32':
	Tool('mingw')(env)
	env.Append(DGREED_LIBS=['SDL', 'opengl32', 'glu32', 'OpenAL32'],
		DGREED_FONT_TOOL=default_font_tool.replace('/', '\\'),
		DGREED_TEST_TOOL=default_test_tool.replace('/', '\\'),
		DGREED_ARENACOMPR_TOOL=default_arenacompr_tool,
		DGREED_DIR_SEPARATOR='\\')
elif str(Platform()) == 'darwin':
	env.Append(DGREED_LIBS=[], DGREED_FONT_TOOL=default_font_tool,
		DGREED_TEST_TOOL=default_test_tool,
		DGREED_ARENACOMPR_TOOL=default_arenacompr_tool,
		DGREED_DIR_SEPARATOR='/',
		FRAMEWORKS=['SDL', 'OpenGL', 'OpenAL', 'Cocoa'])
	
	AddOption('--bundle', dest='bundle', action='store_true', 
		help='compile apps to be placed in Mac OS X bundles')
	if GetOption('bundle'):
		env.Append(CCFLAGS=' -DMACOSX_BUNDLE')

	AddOption('--universal', dest='universal', action='store_true',
		help='build universal (i386 and ppc) Mac OS X binaries')
	if GetOption('universal'):
		env.Append(CCFLAGS=' -arch ppc -arch i386', 
			LINKFLAGS=' -arch ppc -arch i386')
else:
	env.Append(DGREED_LIBS=['SDL', 'GL', 'GLU', 'openal'],
		DGREED_FONT_TOOL=default_font_tool, 
		DGREED_TEST_TOOL=default_test_tool,
		DGREED_ARENACOMPR_TOOL=default_arenacompr_tool,
		DGREED_DIR_SEPARATOR='/')
	
env_dbg = env.Clone()
env_dbg.Prepend(CCFLAGS='-D_DEBUG -g ', DGREED_POSTFIX='d',
	DGREED_LIBS=['dgreedd'])
env_rel = env.Clone()
env_rel.Prepend(CCFLAGS='-DNDEBUG -O2 ', DGREED_POSTFIX='',
	DGREED_LIBS=['dgreed'])

for sub in SUBS:
	build_postfix = '' if sub=='src' else sub+'_'
	env = env_dbg
	env.SConscript(sub + '/SConscript', variant_dir=BUILD_DIR+build_postfix+'dbg',
		duplicate=0, exports='env')
	env = env_rel
	env.SConscript(sub + '/SConscript', variant_dir=BUILD_DIR+build_postfix+'rel',
		duplicate=0, exports='env')

